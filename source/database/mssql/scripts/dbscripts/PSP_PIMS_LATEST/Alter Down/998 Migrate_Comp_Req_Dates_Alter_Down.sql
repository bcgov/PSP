/* -----------------------------------------------------------------------------
Migrate the PIMS_EXPROPRIATION_NOTICE and PIMS_EXPROPRIATION_VESTING dates
to the PIMS_COMPENSATION_REQUISITION table.
. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . 
Author        Date         Comment
------------  -----------  -----------------------------------------------------
Doug Filteau  2024-Jan-29  Initial version.
----------------------------------------------------------------------------- */

SET XACT_ABORT ON
GO
SET TRANSACTION ISOLATION LEVEL SERIALIZABLE
GO
BEGIN TRANSACTION
GO
IF @@ERROR <> 0 SET NOEXEC ON
GO

-- Migrate the EXPROP_NOTICE_SERVED_DT to PIMS_COMPENSATION_REQUISITION
PRINT N'Migrate the EXPROP_NOTICE_SERVED_DT to PIMS_COMPENSATION_REQUISITION'
GO
UPDATE req
SET    req.EXPROP_NOTICE_SERVED_DT    = ntc.EXPROP_NOTICE_SERVED_DT
     , req.CONCURRENCY_CONTROL_NUMBER = req.CONCURRENCY_CONTROL_NUMBER + 1
FROM   PIMS_COMPENSATION_REQUISITION req JOIN
       PIMS_EXPROPRIATION_NOTICE     ntc ON ntc.COMPENSATION_REQUISITION_ID = req.COMPENSATION_REQUISITION_ID
WHERE  ntc.EXPROP_NOTICE_SERVED_DT IS NOT NULL
GO
IF @@ERROR <> 0 SET NOEXEC ON
GO

-- Migrate the EXPROP_VESTING_DT to PIMS_COMPENSATION_REQUISITION
PRINT N'Migrate the EXPROP_VESTING_DT to PIMS_COMPENSATION_REQUISITION'
GO
UPDATE req
SET    req.EXPROP_VESTING_DT          = vst.EXPROP_VESTING_DT
     , req.CONCURRENCY_CONTROL_NUMBER = req.CONCURRENCY_CONTROL_NUMBER + 1
FROM   PIMS_COMPENSATION_REQUISITION req JOIN
       PIMS_EXPROPRIATION_VESTING    vst ON vst.COMPENSATION_REQUISITION_ID = req.COMPENSATION_REQUISITION_ID
WHERE  vst.EXPROP_VESTING_DT IS NOT NULL
GO
IF @@ERROR <> 0 SET NOEXEC ON
GO

COMMIT TRANSACTION
GO
IF @@ERROR <> 0 SET NOEXEC ON
GO
DECLARE @Success AS BIT
SET @Success = 1
SET NOEXEC OFF
IF (@Success = 1) PRINT 'The database update succeeded'
ELSE BEGIN
   IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION
   PRINT 'The database update failed'
END
GO
