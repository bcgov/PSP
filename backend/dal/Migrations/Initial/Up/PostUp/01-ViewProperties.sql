SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- This script provides a way to union both parcels and buildings into a single result of properties.
-- Updating view to support many-to-many parcels and buildings.TEMPORARY
CREATE VIEW [dbo].[PROPERTY_VW] AS
SELECT
    p.[PARCEL_ID] AS [ID]
    , p.[CONCURRENCY_CONTROL_NUMBER] AS [CONCURRENCY_CONTROL_NUMBER]
    , p.[PROPERTY_TYPE_ID] AS [PROPERTY_TYPE_ID]
    , p.[PROPERTY_CLASSIFICATION_ID] AS [CLASSIFICATION_ID]
    , c.[NAME] AS [CLASSIFICATION]
    , p.[AGENCY_ID] AS [AGENCY_ID]
    , ISNULL(pa.[NAME], a.[NAME]) AS [AGENCY]
    , ISNULL(pa.[CODE], a.[CODE]) AS [AGENCY_CODE]
    , (SELECT CASE WHEN pa.[AGENCY_ID] IS NOT NULL
        THEN a.[NAME]
        ELSE NULL
        END) AS [SUB_AGENCY]
    , (SELECT CASE WHEN pa.[AGENCY_ID] IS NOT NULL
        THEN a.[CODE]
        ELSE NULL
        END) AS [SUB_AGENCY_CODE]
    , p.[ADDRESS_ID] AS [ADDRESS_ID]
    , TRIM(ISNULL(adr.[ADDRESS1], '') + ' ' + ISNULL(adr.[ADDRESS2], '')) AS [ADDRESS]
    , adr.[ADMINISTRATIVE_AREA] AS [ADMINISTRATIVE_AREA]
    , ap.[NAME] AS [PROVINCE]
    , adr.[POSTAL] AS [POSTAL]
    , p.[PROJECT_NUMBERS] AS [PROJECT_NUMBERS]
    , p.[NAME] AS [NAME]
    , p.[DESCRIPTION] AS [DESCRIPTION]
    , p.[LOCATION] AS [LOCATION]
    , p.[BOUNDARY] AS [BOUNDARY]
    , p.[IS_SENSITIVE] AS [IS_SENSITIVE]
    , p.[IS_VISIBLE_TO_OTHER_AGENCIES] AS [IS_VISIBLE_TO_OTHER_AGENCIES]

    -- Parcel Properties
    , p.[PID]
    , p.[PIN]
    , p.[LAND_AREA] AS [LAND_AREA]
    , p.[LAND_LEGAL_DESCRIPTION] AS [LAND_LEGAL_DESCRIPTION]
    , p.[ZONING] AS [ZONING]
    , p.[ZONING_POTENTIAL] AS [ZONING_POTENTIAL]

    -- Building Properties
    , p.[PARCEL_ID] AS [PARCEL_ID]
    , 0 AS [BUILDING_CONSTRUCTION_TYPE_ID]
    , null AS [BUILDING_CONSTRUCTION_TYPE]
    , 0 AS [BUILDING_FLOOR_COUNT]
    , 0 AS [BUILDING_PREDOMINATE_USE_ID]
    , null AS [BUILDING_PREDOMINATE_USE]
    , 0 AS [BUILDING_OCCUPANT_TYPE_ID]
    , null AS [BUILDING_OCCUPANT_TYPE]
    , null AS [BUILDING_TENANCY]
    , 0 AS [RENTABLE_AREA]
    , null AS [LEASE_EXPIRY]
    , null AS [OCCUPANT_NAME]
    , CAST(0 AS BIT) AS [TRANSFER_LEASE_ON_SALE]

    , eas.[VALUE] AS [ASSESSED_LAND]
    , eas.[DATE] AS [ASSESSED_LAND_DATE]
    , eim.[VALUE] AS [ASSESSED_BUILDING]
    , eim.[DATE] AS [ASSESSED_BUILDING_DATE]
    , ISNULL(fe.[VALUE], 0) AS [MARKET]
    , fe.[FISCAL_YEAR] AS [MARKET_FISCAL_YEAR]
    , ISNULL(fn.[VALUE], 0) AS [NET_BOOK]
    , fn.[FISCAL_YEAR] AS [NET_BOOK_FISCAL_YEAR]
FROM dbo.[PIMS_PARCEL] p
LEFT JOIN (SELECT DISTINCT [SUBDIVISION_PARCEL_ID] FROM dbo.[PIMS_PARCEL_PARCEL]) sp ON p.[PARCEL_ID] = sp.[SUBDIVISION_PARCEL_ID]
JOIN dbo.[PIMS_PROPERTY_CLASSIFICATION] c ON p.[PROPERTY_CLASSIFICATION_ID] = c.[PROPERTY_CLASSIFICATION_ID]
LEFT JOIN dbo.[PIMS_AGENCY] a ON p.[AGENCY_ID] = a.[AGENCY_ID]
LEFT JOIN dbo.[PIMS_AGENCY] pa ON a.[PARENT_AGENCY_ID] = pa.[AGENCY_ID]
JOIN dbo.[PIMS_ADDRESS] adr ON p.[ADDRESS_ID] = adr.[ADDRESS_ID]
JOIN dbo.[PIMS_PROVINCE] ap ON adr.[PROVINCE_ID] = ap.[PROVINCE_ID]
OUTER APPLY (
    SELECT TOP 1
        [VALUE]
        , [DATE]
    FROM dbo.[PIMS_PARCEL_EVALUATION]
    WHERE [PARCEL_ID] = p.[PARCEL_ID]
        AND [KEY] = 0 -- Assessed Land
    ORDER BY [DATE] DESC
) AS eas
OUTER APPLY (
    SELECT TOP 1
        [VALUE]
        , [DATE]
    FROM dbo.[PIMS_PARCEL_EVALUATION]
    WHERE [PARCEL_ID] = p.[PARCEL_ID]
        AND [KEY] = 2 -- Assessed Building
    ORDER BY [DATE] DESC
) AS eim
OUTER APPLY (
    SELECT TOP 1
        [VALUE]
        , [FISCAL_YEAR]
    FROM dbo.[PIMS_PARCEL_FISCAL]
    WHERE [PARCEL_ID] = p.[PARCEL_ID]
        AND [KEY] = 1 -- Market
    ORDER BY [FISCAL_YEAR] DESC
) AS fe
OUTER APPLY (
    SELECT TOP 1
        [VALUE]
        , [FISCAL_YEAR]
    FROM dbo.[PIMS_PARCEL_FISCAL]
    WHERE [PARCEL_ID] = p.[PARCEL_ID]
        AND [KEY] = 0 -- NetBook
    ORDER BY [FISCAL_YEAR] DESC
) AS fn
UNION ALL
SELECT
    b.[BUILDING_ID] AS [ID]
    , b.[CONCURRENCY_CONTROL_NUMBER] AS [CONCURRENCY_CONTROL_NUMBER]
    , b.[PROPERTY_TYPE_ID] AS [PROPERTY_TYPE_ID]
    , b.[PROPERTY_CLASSIFICATION_ID] AS [ClassificationId]
    , c.[NAME] AS [CLASSIFICATION]
    , b.[AGENCY_ID] AS [AGENCY_ID]
    , ISNULL(pa.[NAME], a.[NAME]) AS [AGENCY]
    , ISNULL(pa.[CODE], a.[CODE]) AS [AGENCY_CODE]
    , (SELECT CASE WHEN pa.[AGENCY_ID] IS NOT NULL
        THEN a.[NAME]
        ELSE NULL
        END) AS [SUB_AGENCY]
    , (SELECT CASE WHEN pa.[AGENCY_ID] IS NOT NULL
        THEN a.[CODE]
        ELSE NULL
        END) AS [SUB_AGENCY_CODE]
    , b.[ADDRESS_ID] AS [ADDRESS_ID]
    , TRIM(ISNULL(adr.[ADDRESS1], '') + ' ' + ISNULL(adr.[ADDRESS2], '')) AS [ADDRESS]
    , adr.[ADMINISTRATIVE_AREA] AS [ADMINISTRATIVE_AREA]
    , ap.[NAME] AS [PROVINCE]
    , adr.[POSTAL] AS [POSTAL]
    , b.[PROJECT_NUMBERS] AS [PROJECT_NUMBERS]
    , b.[NAME] AS [NAME]
    , b.[DESCRIPTION] AS [DESCRIPTION]
    , b.[LOCATION] AS [LOCATION]
    , b.[BOUNDARY] AS [BOUNDARY]
    , b.[IS_SENSITIVE] AS [IS_SENSITIVE]
    , b.[IS_VISIBLE_TO_OTHER_AGENCIES] AS [IS_VISIBLE_TO_OTHER_AGENCIES]

    -- Parcel Properties
    , p.[PID]
    , p.[PIN]
    , p.[LAND_AREA] AS [LAND_AREA]
    , p.[LAND_LEGAL_DESCRIPTION] AS [LAND_LEGAL_DESCRIPTION]
    , p.[ZONING] AS [ZONING]
    , p.[ZONING_POTENTIAL] AS [ZONING_POTENTIAL]

    -- Building Properties
    , p.[PARCEL_ID] AS [PARCEL_ID]
    , b.[BUILDING_CONSTRUCTION_TYPE_ID] AS [BUILDING_CONSTRUCTION_TYPE_ID]
    , bct.[NAME] AS [BUILDING_CONSTRUCTION_TYPE]
    , b.[BUILDING_FLOOR_COUNT] AS [BUILDING_FLOOR_COUNT]
    , b.[BUILDING_PREDOMINATE_USE_ID] AS [BUILDING_PREDOMINATE_USE_ID]
    , bpu.[NAME] AS [BUILDING_PREDOMINATE_USE]
    , b.[BUILDING_OCCUPANT_TYPE_ID] AS [BUILDING_OCCUPANT_TYPE_ID]
    , bot.[NAME] AS [BUILDING_OCCUPANT_TYPE]
    , b.[BUILDING_TENANCY] AS [BUILDING_TENANCY]
    , b.[RENTABLE_AREA] AS [RENTABLE_AREA]
    , b.[LEASE_EXPIRY] AS [LEASE_EXPIRY]
    , b.[OCCUPANT_NAME] AS [OCCUPANT_NAME]
    , b.[TRANSFER_LEASE_ON_SALE] AS [TRANSFER_LEASE_ON_SALE]

    , null AS [ASSESSED_LAND]
    , null AS [ASSESSED_LAND_DATE]
    , eim.[VALUE] AS [ASSESSED_BUILDING]
    , eim.[DATE] AS [ASSESSED_BUILDING_DATE]
    , ISNULL(fe.[VALUE], 0) AS [MARKET]
    , fe.[FISCAL_YEAR] AS [MARKET_FISCAL_YEAR]
    , ISNULL(fn.[VALUE], 0) AS [NET_BOOK]
    , fn.[FISCAL_YEAR] AS [NET_BOOK_FISCAL_YEAR]
FROM dbo.[PIMS_BUILDING] b
LEFT JOIN dbo.[PIMS_PARCEL_BUILDING] pb ON b.[BUILDING_ID] = pb.[BUILDING_ID]
LEFT JOIN dbo.[PIMS_PARCEL] p ON pb.[PARCEL_ID] = p.[PARCEL_ID]
JOIN dbo.[PIMS_PROPERTY_CLASSIFICATION] c ON b.[PROPERTY_CLASSIFICATION_ID] = c.[PROPERTY_CLASSIFICATION_ID]
LEFT JOIN dbo.[PIMS_AGENCY] a ON b.[AGENCY_ID] = a.[AGENCY_ID]
LEFT JOIN dbo.[PIMS_AGENCY] pa ON a.[PARENT_AGENCY_ID] = pa.[AGENCY_ID]
JOIN dbo.[PIMS_ADDRESS] adr ON b.[ADDRESS_ID] = adr.[ADDRESS_ID]
JOIN dbo.[PIMS_PROVINCE] ap ON adr.[PROVINCE_ID] = ap.[PROVINCE_ID]
JOIN dbo.[PIMS_BUILDING_CONSTRUCTION_TYPE] bct ON b.[BUILDING_CONSTRUCTION_TYPE_ID] = bct.[BUILDING_CONSTRUCTION_TYPE_ID]
JOIN dbo.[PIMS_BUILDING_OCCUPANT_TYPE] bot ON b.[BUILDING_OCCUPANT_TYPE_ID] = bot.[BUILDING_OCCUPANT_TYPE_ID]
JOIN dbo.[PIMS_BUILDING_PREDOMINATE_USE] bpu ON b.[BUILDING_PREDOMINATE_USE_ID] = bpu.[BUILDING_PREDOMINATE_USE_ID]
OUTER APPLY (
    SELECT TOP 1
        [VALUE]
        , [DATE]
    FROM dbo.[PIMS_BUILDING_EVALUATION]
    WHERE [BUILDING_ID] = b.[BUILDING_ID]
        AND [KEY] = 0 -- Assessed
    ORDER BY [DATE] DESC
) AS eim
OUTER APPLY (
    SELECT TOP 1
        [VALUE]
        , [FISCAL_YEAR]
    FROM dbo.[PIMS_BUILDING_FISCAL]
    WHERE [BUILDING_ID] = b.[BUILDING_ID]
        AND [KEY] = 1 -- Market
    ORDER BY [FISCAL_YEAR] DESC
) AS fe
OUTER APPLY (
    SELECT TOP 1
        [VALUE]
        , [FISCAL_YEAR]
    FROM dbo.[PIMS_BUILDING_FISCAL]
    WHERE [BUILDING_ID] = b.[BUILDING_ID]
        AND [KEY] = 0 -- NetBook
    ORDER BY [FISCAL_YEAR] DESC
) AS fn
GO
